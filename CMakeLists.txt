cmake_minimum_required(VERSION 2.8.12)
project (Blister C CXX)

find_package(Threads QUIET)
find_program(PROGRAM_CCACHE NAMES ccache PATHS ENV PATH)
find_program(PROGRAM_CPPCHECK NAMES cppcheck PATHS ENV PATH)

if(CMAKE_BUILD_TYPE STREQUAL "")
    set(CMAKE_BUILD_TYPE Debug)
endif()
set(CMAKE_C_STANDARD 11)
set(CMAKE_CXX_STANDARD 11)
set(CMAKE_MACOSX_RPATH 1)
set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/build")
if(POLICY CMP0063)
    cmake_policy(SET CMP0063 NEW)
    set(CMAKE_VISIBILITY_INLINES_HIDDEN 1)
    set(CMAKE_C_VISIBILITY_PRESET hidden)
    set(CMAKE_CXX_VISIBILITY_PRESET hidden)
endif()

if(CMAKE_VERSION VERSION_GREATER_EQUAL 3.9)
    cmake_policy(SET CMP0069 NEW)
    include(CheckIPOSupported)
    check_ipo_supported(RESULT ipo)
    if(ipo AND NOT CMAKE_BUILD_TYPE STREQUAL Debug)
        set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
    endif()
endif()

# compile optimizers
if(NOT APPLE)
    include(cotire OPTIONAL)
endif()
if(COMMAND cotire)
    if (WIN32)
        add_definitions(-DNOIME -DNOMCX -DNOSERVICE -DWIN32_LEAN_AND_MEAN)
    endif()
    set(COTIRE_MAXIMUM_NUMBER_OF_UNITY_INCLUDES -j)
    set(COTIRE_MINIMUM_NUMBER_OF_TARGET_SOURCES 4)
    if(TARGET clean_cotire)
        set(CLEAN_COTIRE clean_cotire)
    endif()
    set_property(DIRECTORY PROPERTY COTIRE_ADD_UNITY_BUILD FALSE)
else()
    function(cotire)
    endfunction()
endif()
if(PROGRAM_CCACHE)
    if(CMAKE_VERSION VERSION_GREATER_EQUAL 3.4)
        set(CMAKE_C_COMPILER_LAUNCHER "${PROGRAM_CCACHE}")
        set(CMAKE_CXX_COMPILER_LAUNCHER "${PROGRAM_CCACHE}")
    else()
        set_property(GLOBAL PROPERTY RULE_LAUNCH_COMPILE "${PROGRAM_CCACHE}")
    endif()
endif()

# CXX CMAKE_BUILD_TYPE flags are not always set
if(NOT CMAKE_CXX_FLAGS_DEBUG AND NOT CMAKE_CXX_FLAGS_RELEASE)
    set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG}")
    set(CMAKE_CXX_FLAGS_MINSIZEREL "${CMAKE_C_FLAGS_MINSIZEREL}")
    set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE}")
    set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_C_FLAGS_RELWITHDEBINFO}")
endif()

if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    if(NOT SANITIZE_FLAGS)
        set(SANITIZE_FLAGS "-D_FORTIFY_SOURCE=2")
    endif()
    set(COMMON_C_FLAGS "${SANITIZE_FLAGS} -pipe -Wall -Wconversion -Wno-deprecated -Werror -Wextra -Wformat=2 -Wno-format-nonliteral -Wno-format-y2k -Wsign-conversion -Wstrict-aliasing -Wstrict-overflow=2 -Wno-unknown-pragmas -Wwrite-strings")
    if(CMAKE_COMPILER_IS_GNUCXX)
        set(CMAKE_EXE_LINKER_FLAGS "${SANITIZE_FLAGS} -Wl,-gc-sections")
        set(CMAKE_SHARED_LINKER_FLAGS "-Wl,-gc-sections")
        if(CMAKE_CXX_COMPILER_VERSION VERSION_LESS_EQUAL 4.6)
            set(COMMON_C_FLAGS "${COMMON_C_FLAGS} -D__USE_XOPEN2K8")
        elseif(CMAKE_CXX_COMPILER_VERSION VERSION_GREATER_EQUAL 5.0)
            set(COMMON_C_FLAGS "${COMMON_C_FLAGS} -Wdouble-promotion -Wlogical-op -Wodr -Wshadow")
        endif()
        if(CMAKE_CXX_COMPILER_VERSION VERSION_GREATER_EQUAL 6.0)
            set(COMMON_C_FLAGS "${COMMON_C_FLAGS} -Wduplicated-cond -Wnull-dereference -Wstringop-overflow=3 -Wsuggest-final-methods -Wsuggest-final-types")
        endif()
        set(COMMON_C_FLAGS_RELWITHDEBINFO "-femit-struct-debug-reduced -ggdb1")
    else()
        set(COMMON_C_FLAGS "${COMMON_C_FLAGS} -fno-standalone-debug -Wdouble-promotion -Wnull-dereference -Wodr -Wshadow")
        set(COMMON_C_FLAGS_RELWITHDEBINFO "-gline-tables-only")
    endif()
    set(COMMON_C_FLAGS_DEBUG "-O1 -fno-omit-frame-pointer -fno-optimize-sibling-calls -fstack-protector -ggdb -D_DEBUG")
    set(COMMON_C_FLAGS_MINSIZEREL "-fdata-sections -ffunction-sections")
    set(COMMON_C_FLAGS_RELEASE "-fdata-sections -ffunction-sections")
    set(COMMON_C_FLAGS_RELWITHDEBINFO "${COMMON_C_FLAGS_RELEASE} ${COMMON_C_FLAGS_RELWITHDEBINFO}")
    set(COMMON_CXX_FLAGS "${COMMON_C_FLAGS} -fno-exceptions -fno-rtti -Woverloaded-virtual")
    set(COMMON_CXX_FLAGS_DEBUG "${COMMON_C_FLAGS_DEBUG}")
    set(COMMON_CXX_FLAGS_MINSIZEREL "${COMMON_C_FLAGS_MINSIZEREL}")
    set(COMMON_CXX_FLAGS_RELEASE "${COMMON_C_FLAGS_RELEASE}")
    set(COMMON_CXX_FLAGS_RELWITHDEBINFO "${COMMON_C_FLAGS_RELWITHDEBINFO}")
endif()

set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${COMMON_C_FLAGS}")
set(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} ${COMMON_C_FLAGS_DEBUG}")
set(CMAKE_C_FLAGS_MINSIZEREL "${CMAKE_C_FLAGS_MINSIZEREL} ${COMMON_C_FLAGS_MINSIZEREL}")
set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} ${COMMON_C_FLAGS_RELEASE}")
set(CMAKE_C_FLAGS_RELWITHDEBINFO "${CMAKE_C_FLAGS_RELWITHDEBINFO} ${COMMON_C_FLAGS_RELWITHDEBINFO}")
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${COMMON_CXX_FLAGS}")
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} ${COMMON_CXX_FLAGS_DEBUG}")
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} ${COMMON_CXX_FLAGS_RELEASE}")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO} ${COMMON_CXX_FLAGS_RELWITHDEBINFO}")

# cppcheck support
list(APPEND cppcheck_args --enable=warning,style,performance,portability,information,missingInclude --force --inline-suppr -j 4 -q --template="{file}:{line} {severity}={id} {message}")
list(APPEND cppcheck_suppressions ConfigurationNotChecked cstyleCast noExplicitConstructor postfixOperator preprocessorErrorDirective uninitMemberVar unmatchedSuppression unreadVariable variableScope)
foreach(suppression ${cppcheck_suppressions})
    list(APPEND cppcheck_args --suppress=${suppression})
endforeach()

function(target_cppcheck target)
    if(NOT TARGET ${target})
        message(FATAL_ERROR "cppcheck target does not exist: ${target}")
    elseif(PROGRAM_CPPCHECK)
        get_target_property(includes ${target} INCLUDE_DIRECTORIES)
        set(cppcheck_includes)
        foreach(include ${includes})
	    if(include)
		list(APPEND cppcheck_includes "-I${include}")
	    endif()
        endforeach()
        get_target_property(sources ${target} SOURCES)
        set(cppcheck_files)
        foreach(source ${sources})
            get_source_file_property(cppcheck_lang ${source} LANGUAGE)
            get_source_file_property(cppcheck_loc ${source} LOCATION)
            if(cppcheck_lang AND (cppcheck_lang STREQUAL C OR cppcheck_lang STREQUAL CXX))
                list(APPEND cppcheck_files ${cppcheck_loc})
            endif()
        endforeach()
        add_custom_target(${target}_cppcheck
            COMMAND ${PROGRAM_CPPCHECK} ${cppcheck_args} ${cppcheck_includes} ${cppcheck_files}
            WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
            COMMENT "Running cppcheck on ${target}" VERBATIM)
        add_dependencies(cppcheck ${target}_cppcheck)
    endif()
endfunction()

function(cppcheck)
    foreach(target ${ARGN})
        target_cppcheck(${target})
    endforeach()
endfunction()

if(PROGRAM_CPPCHECK)
    add_custom_target(cppcheck COMMENT "C/C++ static code analysis")
else()
    add_custom_target(cppcheck COMMENT "cppcheck not available")
endif()
set_target_properties(cppcheck PROPERTIES EXCLUDE_FROM_ALL TRUE)

# distclean equivalent
add_custom_target(distclean)
add_custom_command(
    COMMENT "Cleaning distribution"
    COMMAND $(MAKE) ${CLEAN_COTIRE} clean
    COMMAND find . -name CMakeFiles -o -name CMakeCache.txt -o -name cmake_install.cmake -o -name \\*_cotire.cmake -o -name \\*.ninja -o -name Makefile | xargs rm -rf
    TARGET distclean)

add_subdirectory(lib)
add_subdirectory(test)

